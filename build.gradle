plugins {
    id 'java'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
}

group = 'io.github.steelwoolmc'
version = version + '+' + minecraft_version

minecraft {
    mappings channel: 'official', version: minecraft_version
}

configurations {
    shadedImplementation { transitive = false }
    implementation.extendsFrom(shadedImplementation)
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    shadedImplementation "net.fabricmc:sponge-mixin:${mixin_version}"
}

jar {
    archiveClassifier = "slim"
}

shadowJar {
    from("licenses") {
        into "META-INF/"
    }
    from("LICENSE.txt") {
        into "META-INF/"
    }
    // Exclude dependency licenses since we include them ourselves
    exclude "**/LICENSE*"
    exclude "**/NOTICE*"
    // Relocate mixin packages
    relocate "org.spongepowered", "io.github.steelwoolmc.shadow.spongepowered"
    relocate "shadowignore.org.spongepowered", "org.spongepowered"
    // Relocate service files
    mergeServiceFiles {
        // Remove mixin's transformation service
        exclude "META-INF/services/cpw.mods.modlauncher.api.ITransformationService"
    }
    // Remove mixin's launch plugin service
    exclude "META-INF/services/cpw.mods.modlauncher.serviceapi.ILaunchPluginService"

    archiveClassifier = ""
    configurations = [project.configurations.shadedImplementation]
}

assemble.dependsOn shadowJar

// Replace published jar artifact with shadowJar 
configurations {
    [runtimeElements, apiElements].each {
        it.outgoing {
            artifacts.clear()
            artifact(shadowJar)
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            // Remove forge dependency from pom, disable gradle module metadata
            fg.component it
        }
    }
}
